##########################
# Logfile help
<<
"" PRINT
"    less: Invoke less ona filename list element" PRINT
"          filename idx less CALL => " PRINT
"" PRINT
"     vim: Invoke vim on a filename list element" PRINT
"          filename idx vim CALL =>" PRINT
"" PRINT
"    show: Print a list in a numbered output" PRINT
"          [list] show CALL => " PRINT
"" PRINT
"  sstack: Save the current stack" PRINT
"          objn objn-1 obj \"name\" sstack CALL =>" PRINT
"" PRINT
"  rstack: Restore a saved stack" PRINT
"          \"name\" rstack CALL => objn obj-1 obj" PRINT
"" PRINT
"   vedit: Edit an object" PRINT
"          obj vedit CALL => obj" PRINT
"" PRINT
"    view: view, via vim readonly an object" PRINT
"          obj view CALL => obj" PRINT
"" PRINT
>>
help STO

#Invoke less on a filename list element
#[...]
# idx
<< idx STOL
l STOL
"less "
l RCLL
idx RCLL
GET
CAT
!
>>
less STO

#####################################################
#Invoke vim on a filename list element
#[...]
# idx
<< idx STOL
l STOL
"vim "
l RCLL
idx RCLL
GET
CAT
!
l RCLL
>>
vim STO

100 showwidth STO # default width for show

# Format list items into a numbers output
<< 
lst STOL
0 count STOL
lst RCLL
FOR
    "%{count}: %0" FORMAT 0 showwidth RCL SUBSTR PRINT
    DROP  # drop the FOR object
    count RCLL INC count STOL
ENDFOR
>> show STO

#############################################
# Save the current stack
# objn objn-1 obj name sstack CALL =>
<<
"tmp" STOL  # save the name
DEPTH
TOLIST
"tmp" RCLL
STO
>> sstack STO

#############################################
# Restore saved stack
# name rstack CALL => objn objn-1 obj
<<
RCL
FROMLIST
>> rstack STO

############################################
# Vim edit an object
# obj vim CALL => [list]
<<
"rpstmp.txt" FSAVE
"vim rpstmp.txt" SYSTEM
"rpstmp.txt" FRESTORE
#"rm rpstmp.txt" SYSTEM
>> vedit STO

############################################
# View edit an object
# obj view CALL => [list]
<<
"rpstmp.txt" FSAVE
"view rpstmp.txt" SYSTEM
"rm rpstmp.txt" SYSTEM
>> view STO

###########################################
# Less 
# [list] less CALL => [list]
<<
DUP
"rpstmp.txt" FWRITE
"less rpstmp.txt" SYSTEM
"rm rpstmp.txt" SYSTEM
>> less STO

